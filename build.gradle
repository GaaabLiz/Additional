buildscript {
    configurations.classpath {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

plugins {
    id "dev.architectury.loom" version "${dev_architectury_loom_version}" apply false
    id "io.github.juuxel.loom-quiltflower" version "$quilt_flower_version" apply false
    id "architectury-plugin" version "$architectury_plugin_version"
    id "me.shedaniel.unified-publishing" version "$unified_publishing_version" apply false
    id "maven-publish"
}



architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    archivesBaseName = "${rootProject.name}-${project.name}"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
        /*
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.18.2:${rootProject.parchment_version}")
        }*/
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "architectury-plugin"

    version = "${rootProject.mod_version}+${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        maven { url "https://repo.moonflower.gg/releases" }
        maven { url "https://maven.parchmentmc.org" }
        maven { url "https://maven.terraformersmc.com/" }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(17)
        }
    }

    ext {
        createChangelog = {
            def changes = new StringBuilder()
            changes << "$rootProject.mod_name $rootProject.mod_version for $project.supported_version\n"
            def proc = "git log --max-count=200 --pretty=format:%s".execute()
            proc.in.eachLine { line ->
                def processedLine = line.toString()
                if (!processedLine.toLowerCase().contains("translations") &&
                        !processedLine.toLowerCase().contains("merge") &&
                        !processedLine.toLowerCase().contains("branch")
                ) {
                    changes << "\n- ${processedLine.capitalize()}"
                }
            }
            proc.waitFor()
            return changes.toString()
        }
    }

    javadoc {
        failOnError = false
    }

    java {
        withSourcesJar()
    }
}